// Top-level build file where you can add configuration options common to all sub-projects/modules.
ext {
    compileSdkVersion = 26
    buildToolsVersion = "26.0.1"
    targetSdkVersion = 26
    minSdkVersion = 26
    supportVersion = "26.0.1"
    thingsVersion = "0.5-devpreview"
    espressoVersion = "3.0.0"
    testRunnerVersion = "1.0.0"
    mockitoVersion = "2.8.9"
    annotationsVersion = "4.3.1"
    constraintLayoutVersion = "1.0.2"
    version = getTheVersion()
    buildNumber = getTheBuildNumber()

    bintrayRepo = 'maven'
    publishedGroupId = 'nz.org.winters'
    siteUrl = 'https://github.com/wintersandroid/Android-Things-PCA9685'
    artifact = "libPCA9685"
    libraryDescription = "PCA9685 Servo Lib for Android Things"
    libraryVersion = getTheVersion()
}

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com'
        }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-beta1'
        classpath 'com.novoda:bintray-release:0.5.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com'
        }

    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def static getBuildMajor(){
    return 0
}

def getTheVersion() {
    def buildMajor = getBuildMajor()
    def buildNumber = getTheBuildNumber()
    if( buildNumber == 0)
    {
        return "${buildMajor}.${buildNumber}-SNAPSHOT"

    }else{
        return "${buildMajor}.${buildNumber}"
    }

}

def getTheBuildNumber(){
    return hasProperty('BUILD_NUMBER') ? BUILD_NUMBER.toInteger() : 0
}

def getBintrayUser(){
    return hasProperty('BINTRAY_USER') ? BINTRAY_USER : ""
}

def getBintrayApiKey(){
    return hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ""
}
